apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def ext = rootProject.ext
    compileSdkVersion ext.android.targetSdk
    defaultConfig {
        applicationId ext.application.id
        minSdkVersion ext.android.minSdk
        targetSdkVersion ext.android.targetSdk
        versionCode ext.application.versionCode
        versionName ext.application.versionName
        testInstrumentationRunner ext.application.testInstrumentationRunner
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    dexOptions {
        // Skip pre-dexing when disabled via -DpreDex=false.
        preDexLibraries ext.build.preDexEnabled
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        test.java.srcDirs += 'src/test/java'
        androidTest.java.srcDirs += 'src/androidTest/java'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'androidsupportmultidexversion.txt'
    }
}

def POSTER_BASE_URL = System.getenv("POSTER_BASE_URL") ?: posterBaseUrl

android.buildTypes.each { type ->
    type.buildConfigField "String", "POSTER_BASE_URL", "\"$POSTER_BASE_URL\""
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':domain')
    implementation project(':data')

    // UI
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.vectordrawable:vectordrawable:$vectorDrawableVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "com.github.lzyzsd:circleprogress:$circleprogressVersion"
    implementation "com.google.android:flexbox:$flexboxLayoutVersion"
    implementation "com.pierfrancescosoffritti.androidyoutubeplayer:core:$androidyoutubeplayerVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$compiler.kotlin"
    implementation "androidx.core:core-ktx:$androidKtxVersion"

    // Multidex
    implementation "androidx.multidex:multidex:$multidexVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Unit tests
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"

    // Instrumentation tests
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.annotation:annotation:$annotationVersion"
}
